// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
    }

    ext {
        androidToolsGradleVersion = "3.5.2"
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidToolsGradleVersion"
        // NOTE: Do not place your application dependencies here; they belong in the individual module build.gradle
        // files
    }
}

configure([
    project("sdk:core:azure-core"),
    project("sdk:storage:azure-storage-blob")]) {

    apply plugin: "com.android.library"
    apply plugin: "maven"

    task sourcesJar(type: Jar) { -> project
        from android.sourceSets.main.java.srcDirs
        classifier = "sources"
    }

    artifacts {
        archives sourcesJar
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    ext {
        annotationsVersion = "1.1.0"
        jacksonDatabindVersion = "2.9.9"
        jacksonDataFormatXmlVersion = "2.9.9"
        jUnitVersion = "4.12"
        mockWebServerVersion = "3.13.1"
        okHttpVersion = "3.13.1"
        retrofitVersion = "2.6.2"
        staxApiVersion = "1.0-2"
        threeTenAbpVersion = "1.2.1"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Synthesize dependencies for service-level projects so that running a task on a service-level project automatically
// runs it on all subprojects of that project
configure(subprojects.findAll { it.path.count(':') == 2 }) {
    project.gradle.startParameter.taskNames.each { task ->
        task = task.split(':').last()

        tasks.create(task) {
            subprojects.each { dependsOn("$it.name:$task") }
        }
    }
}
