ext.publishName = "Microsoft Azure Android Client Library for Communication Chat Service"
description = "This package contains the Android client API code for Microsoft Azure Communication Chat Service.\n" +
    "    For this release, see notes - https://github.com/Azure/azure-sdk-for-android/blob/master/sdk/communication/azure-communication-chat/README.md and https://github.com/Azure/azure-sdk-for-android/blob/master/sdk/communication/azure-communication-chat/CHANGELOG.md."
version = "1.0.0-beta.8"
ext.versionCode = 1

android {
    defaultConfig {
        versionCode project.versionCode
        versionName project.version

        def buildVariant = getBuildVariant()
        def envPropertiesFile
        if ((buildVariant == "Prod")) {
            envPropertiesFile = "env-prod.properties"
        } else if ((buildVariant == "Test")) {
            envPropertiesFile = "env-test.properties"
        } else {
            envPropertiesFile = "env-prod.properties"
        }

        def envProperties = new Properties()
        envProperties.load(new FileInputStream("$projectDir/" + envPropertiesFile))
        println("buildVariant = $buildVariant")
        for (String key : envProperties.keySet()) {
            buildConfigField "String", key.replaceAll("\\.", "_").toUpperCase(), envProperties[key]
        }
    }
}

static def getBuildVariant() {
    return "Prod"
}


dependencies {
    api "com.azure.android:azure-core:1.0.0-beta.3"
    implementation 'com.microsoft.trouter:client-android:0.0.1-beta.1'
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jacksonVersion"
    implementation "com.jakewharton.threetenabp:threetenabp:$threeTenAbpVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation project(path: ':sdk:communication:azure-communication-common')
    testImplementation "junit:junit:$jUnitVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
    testImplementation "com.jakewharton.threetenabp:threetenabp:$threeTenAbpVersion"
    testImplementation "org.threeten:threetenbp:$threeTenBpVersion"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"

}
